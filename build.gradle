plugins {
	id 'org.springframework.boot' version '2.5.6'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'war'
	id 'org.hidetake.swagger.generator' version '2.18.2'
}

group = 'be.projetSGBD'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	
		//A Ajouter
	swaggerCodegen 'org.openapitools:openapi-generator-cli:4.3.1'
	compileOnly 'io.swagger:swagger-annotations:1.6.2'
	compileOnly 'org.springframework.boot:spring-boot-starter-validation'
	compileOnly 'org.openapitools:jackson-databind-nullable:0.2.1'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	
	//DB
    runtimeOnly 'com.microsoft.sqlserver:mssql-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    
    implementation "io.springfox:springfox-boot-starter:3.0.0"
	implementation "io.springfox:springfox-swagger-ui:3.0.0"
}


swaggerSources {
  def typeMappings = 'URI=URI'
  def importMappings = 'URI=java.net.URI'
  projetSGBD {
    def apiYaml = "${rootDir}/src/main/resources/api/openapi.yaml"
    def configJson = "${rootDir}/src/main/resources/api/config.json"
    inputFile = file(apiYaml)
    def ignoreFile = file("${rootDir}/src/main/resources/api/.openapi-generator-ignore")
    code {
      language = 'spring'
      configFile = file(configJson)
      rawOptions = ['--ignore-file-override', ignoreFile, '--type-mappings',
										typeMappings, '--import-mappings', importMappings] as List<String>
      components = [models: true, apis: true, supportingFiles: 'ApiUtil.java']
      //dependsOn validation // Should be uncommented once plugin starts supporting OA 3 validation
    }
  }
}

compileJava.dependsOn swaggerSources.projetSGBD.code
sourceSets.main.java.srcDir "${swaggerSources.projetSGBD.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.projetSGBD.code.outputDir}/src/main/resources"

test {
	useJUnitPlatform()
}
